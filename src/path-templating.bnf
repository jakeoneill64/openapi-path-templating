; OpenAPI Path Templating ABNF syntax
; variant of https://datatracker.ietf.org/doc/html/rfc3986#section-3.3

path-template = slash *( path-segment slash ) [ path-segment ] [ question-mark query-string ]
path-segment  = 1*( path-literal / template-expression )
path-literal  = 1*pchar
template-expression = "{" template-expression-param-name "}"
template-expression-param-name = 1*( %x00-7A / %x7C / %x7E-10FFFF )

query-string  = query-param *( "&" query-param )
query-param   = key "=" value
key           = 1*( ALPHA / DIGIT / "-" / "." / "_" / "~" )
value         = *( ALPHA / DIGIT / "-" / "." / "_" / "~" / "%" / "!" / "*" / "'" / "(" / ")" / "," )

slash = "/"
question-mark = "?"

pchar = unreserved / pct-encoded / sub-delims / ":" / "@"
unreserved = ALPHA / DIGIT / "-" / "." / "_" / "~"
pct-encoded = "%" HEXDIG HEXDIG
sub-delims  = "!" / "$" / "&" / "'" / "(" / ")" / "*" / "+" / "," / ";" / "="

ALPHA = %x41-5A / %x61-7A
DIGIT = %x30-39
HEXDIG = DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
